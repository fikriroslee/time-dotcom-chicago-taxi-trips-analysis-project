config {
    type: "table",
    schema: "marts",
    description: "Analysis of trip patterns on US holidays vs weekends"
}

-- Initialising Common Table Expressions (CTEs)
-- CTE 1: Define major US holidays for 2023
WITH us_holidays AS (
    SELECT
        holiday_date,
        holiday_name
    FROM UNNEST([
        -- 2023 holidays
        STRUCT(DATE '2023-01-01' AS holiday_date, 'New Year' AS holiday_name),
        STRUCT(DATE '2023-01-02', 'New Year (Observed)'),
        STRUCT(DATE '2023-07-04', 'Independence Day'),
        STRUCT(DATE '2023-11-23', 'Thanksgiving'),
        STRUCT(DATE '2023-11-24', 'Day After Thanksgiving'),
        STRUCT(DATE '2023-12-25', 'Christmas')
    ])
),

-- CTE 2a: Flag holidays and weekends in 2023
daily_with_flags AS (
    SELECT
        stt.*,
        CASE
            WHEN day_of_week  IN (1, 7) THEN TRUE
            ELSE FALSE
        END AS is_weekend,
        CASE
            WHEN trip_date IN (SELECT holiday_date FROM us_holidays) THEN TRUE
            ELSE FALSE
        END AS is_holiday,
        ush.holiday_name
    FROM
        ${ref('staging_taxi_trips')} AS stt
    LEFT JOIN
        us_holidays AS ush
            ON stt.trip_date = ush.holiday_date
),

-- CTE 2b: Flag regular/holiday weekdays/weekends in 2023
daily_classified_metrics AS (
    SELECT
        trip_date,
        is_weekend,
        is_holiday,
        holiday_name,
        CASE
            WHEN is_holiday = TRUE AND is_weekend = TRUE THEN 'Holiday Weekend'
            WHEN is_holiday = TRUE AND is_weekend = FALSE THEN 'Holiday Weekday'
            WHEN is_holiday = FALSE AND is_weekend = TRUE THEN 'Regular Weekend'
            WHEN is_holiday = FALSE AND is_weekend = FALSE THEN 'Regular Weekday'
        END AS day_type,
        COUNT(*) AS trip_count,
        ROUND(AVG(trip_miles), 2) AS avg_trip_miles,
        ROUND(SUM(fare), 2) AS total_fare,
        ROUND(AVG(fare), 2) AS avg_fare,
        ROUND(SUM(tips), 2) AS total_tips,
        ROUND(AVG(tips), 2) AS avg_tips
    FROM daily_with_flags
    GROUP BY trip_date, is_weekend, is_holiday, holiday_name, day_type
),

-- CTE 3: Calculate average of trip metrics by day_type
trip_metric_averages AS (
    SELECT
        day_type,
        ROUND(AVG(trip_count), 0) AS avg_trip_count_by_day_type,
        ROUND(AVG(avg_fare), 2) AS avg_fare_by_day_type,
        ROUND(AVG(avg_tips), 2) AS avg_tips_by_day_type
    FROM daily_classified_metrics
    GROUP BY day_type
)

-- Main Query: Showcase holiday vs weekend impact on trips in 2023
SELECT
    dcm.trip_date,
    dcm.is_weekend,
    dcm.is_holiday,
    dcm.day_type,
    dcm.holiday_name,
    dcm.trip_count,
    dcm.avg_trip_miles,
    dcm.total_fare,
    dcm.avg_fare,
    dcm.total_tips,
    dcm.avg_tips,
    -- Compare to average of trip metrics by day_type
    ROUND(dcm.trip_count - tma.avg_trip_count_by_day_type, 2) AS trip_count_per_day_vs_avg_trip_count_by_day_type,
    ROUND((dcm.trip_count - tma.avg_trip_count_by_day_type) / tma.avg_trip_count_by_day_type * 100, 2) AS trip_count_pct_change,
    ROUND(dcm.avg_fare - tma.avg_fare_by_day_type, 2) AS avg_fare_per_day_vs_avg_fare_by_day_type,
    ROUND(dcm.avg_tips - tma.avg_tips_by_day_type, 2) AS avg_tips_per_day_vs_avg_tips_by_day_type
FROM
    daily_classified_metrics AS dcm
INNER JOIN
    trip_metric_averages AS tma
        ON dcm.day_type = tma.day_type
ORDER BY
    dcm.trip_date DESC